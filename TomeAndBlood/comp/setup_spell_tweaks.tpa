
////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////


// -----------------------------
// SPELL TWEAKS: ILLUSIONARY CLONES
// -----------------------------

// This component uses code by Subtledoctor and is used with permission of the original author. 


DEFINE_ACTION_FUNCTION illusion_clone_tweaks BEGIN 


//updated illusionary clone spells___________________________________________________
//
LAF d5_resolve_state STR_VAR new_state_id = ~D5_MISLEAD~ RET new_state_ind END
OUTER_SET mislead_clone_state = %new_state_ind%

LAF d5_resolve_state STR_VAR new_state_id = ~D5_L_SIMULACRUM~ RET new_state_ind END
OUTER_SET lesser_simulacrum_state = %new_state_ind%

//LAF d5_resolve_state STR_VAR new_state_id = ~D5_SIMULACRUM~ RET new_state_ind END
//OUTER_SET simulacrum_state = %new_state_ind%

ACTION_IF FILE_EXISTS_IN_GAME ~spwi804.spl~ BEGIN

	OUTER_SET spell_ind = 0
	COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
	  READ_SHORT 0x1c spell_type
	  PATCH_IF (spell_type < 3) BEGIN
		READ_LONG 0x34 spell_level
		PATCH_IF (spell_level > 1) AND (spell_level < 5) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_lowspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
		PATCH_IF (spell_level > 4) AND (spell_level < 8) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_midspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
		PATCH_IF (spell_level > 7) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_highspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
	  END
	BUT_ONLY

//...apply penalties to simulacra
//
	COPY ~TomeAndBlood/data/spell_tweaks/simulacr.spl~ ~override~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 337 target = 1 parameter1 = ~-1~ parameter2 = 237	END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 9 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 11 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 12 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %mislead_clone_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul6~ END // and make d5simul6 318 immune if stat = 3
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %lesser_simulacrum_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul7~ END // and make d5simul7 318 immune if stat = 3
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %simulacrum_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~d5simul8~ END
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 72 target = 2 parameter1 = 4 parameter2 = 0 timing = 1 END 
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 241 target = 2 parameter1 = 0 parameter2 = 1002 duration = 1 END 

//...custom simulacrum summon
//
	COPY ~TomeAndBlood/data/spell_tweaks/d5si804.spl~ ~override~
	  SAY NAME1 ~ ~
	  SAY UNIDENTIFIED_DESC ~ ~

//...5th level: nerfed mislead (imp. invisibility, no super invisibility. and 50% hp)
//
	COPY_EXISTING ~spwi607.spl~ ~override~
	  READ_LONG 0x08 mislead_name
	  READ_LONG 0x50 mislead_desc
	BUT_ONLY
	COPY ~TomeAndBlood/data/spell_tweaks/spwi607.spl~ ~override/spwi505.spl~
	  WRITE_LONG 0x08 %mislead_name%
	  WRITE_LONG 0x50 %mislead_desc%
	  WRITE_LONG 0x34 5
	  WRITE_ASCII 0x3a ~spwi505c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 STR_VAR icon = ~spwi505b~ END
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505a.spl~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505a.eff~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505b.spl~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/mislead.spl~ ~override~

//...6th level: shadow clone
//
	COPY_EXISTING ~spwi804.spl~ ~override/spwi607.spl~
	  SAY NAME1 @6271
	  SAY UNIDENTIFIED_DESC @6272
	  WRITE_LONG 0x34 6
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %mislead_clone_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 2 STR_VAR resource = ~spwi607~ END
	  WRITE_ASCII 0x3a ~spwi607c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 STR_VAR icon = ~spwi607b~ END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl7k.itm~ BEGIN
	  COPY_EXISTING ~scrl7k.itm~ ~override~
	  SAY NAME2 @6271
	  SAY IDENTIFIED_DESC @6272
	  WRITE_ASCII 0x3a ~spwi703a~ #8
	  LPF ALTER_ITEM_HEADER STR_VAR icon = ~spwi703a~ END
	END 

//...7th level: lesser simulacrum
	COPY_EXISTING ~spwi804.spl~ ~override/spwi703.spl~
	  SAY NAME1 @6273
	  SAY UNIDENTIFIED_DESC @6274
	  WRITE_LONG 0x34 7
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %lesser_simulacrum_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~spwi206~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 22 STR_VAR resource = ~spwi703~ END
	  WRITE_ASCII 0x3a ~spwi703c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 2 STR_VAR icon = ~spwi703b~ END

	ACTION_IF FILE_EXISTS_IN_GAME ~scrl8f.itm~ BEGIN
	  COPY_EXISTING ~scrl8f.itm~ ~override~
	  SAY NAME2 @6273
	  SAY IDENTIFIED_DESC @6274
	  WRITE_ASCII 0x3a ~spwi607a~ #8
	  LPF ALTER_ITEM_HEADER STR_VAR icon = ~spwi607a~ END
	END 

//...9th level: projected simulacrum
//
	COPY_EXISTING ~d5si804.spl~ ~override/d5si8049.spl~
	  LPF ALTER_SPELL_HEADER INT_VAR target = 1 END
	  LPF ALTER_EFFECT INT_VAR silent = 1 target = 2 END
	  LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
	  LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 272 target = 2 timing = 9 STR_VAR resource = ~d5si9ilb~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 4 duration = 2 STR_VAR resource = ~d5si8049~ END
	COPY_EXISTING ~spwi804.spl~ ~override/d5si9il.spl~
	  SAY NAME1 @6277
	  SAY UNIDENTIFIED_DESC @6278
	  WRITE_LONG 0x34 9
	  WRITE_ASCII 0x3a ~d5projic~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 target = 1 range = 40 STR_VAR icon = ~d5projib~ END
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si8049~ END
	COPY ~TomeAndBlood/data/spell_tweaks/d5projic.bam~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5projib.bam~ ~override~
	ADD_SPELL ~override/d5si9il.spl~ 2 9 D5_WIZARD_CLONE

	CREATE EFF ~d5si9ilb~
	  WRITE_LONG 0x10 146
	  WRITE_LONG 0x14 1
	  WRITE_LONG 0x1c 0
	  WRITE_LONG 0x20 1
	  WRITE_LONG 0x24 1
	  WRITE_SHORT 0x2c 100
	  WRITE_ASCII 0x30 ~d5si9ilb~ #8

	COPY ~TomeAndBlood/lib/d5_base.spl~ ~override/d5si9ilb.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 72 target = 2 parameter1 = 4 parameter2 = 0 timing = 1 END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 241 target = 2 parameter1 = 0 parameter2 = 1002 duration = 1 END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 4 duration = 2 STR_VAR resource = ~d5si8049~ END

	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~D5_WIZARD_CLONE~
		RET spell_res
	END

	COPY_EXISTING ~%spell_res%.spl~ ~override~
	  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 321 STR_VAR match_resource = ~d5si9il~ resource = EVAL ~%spell_res%~ END

	ACTION_IF (FILE_EXISTS_IN_GAME ~hidespl.2da~) AND (GAME_IS ~iwdee~) BEGIN
		APPEND ~hidespl.2da~ ~%spell_res%	1		0~
	END
	ACTION_IF (FILE_EXISTS_IN_GAME ~hidespl.2da~) AND !(GAME_IS ~iwdee~) BEGIN
		APPEND ~hidespl.2da~ ~%spell_res%	1		0		0~
	END

	ACTION_IF (FILE_EXISTS_IN_GAME ~clabma08.2da~) BEGIN	//	illusionist kit table
	  APPEND ~clabma08.2da~ ~PROJ_CLONE  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        GA_%spell_res%  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~
	END

//...8th level: simulacrum
//
	COPY_EXISTING ~spwi804.spl~ ~override~
	  SAY NAME1 @6275
	  SAY UNIDENTIFIED_DESC @6276
	  LPF DELETE_EFFECT END
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %simulacrum_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~spwi405~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 2 STR_VAR resource = ~spwi804~ END
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl8z.itm~ BEGIN
	  COPY_EXISTING ~scrl8z.itm~ ~override~
	  SAY NAME2 @6275
	  SAY IDENTIFIED_DESC @6276
	END 

//...apply penalties to simulacra, part 2
//
	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul6.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 7) parameter2 = 0 timing = 9 END 	//	-7 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 7) parameter2 = 0 timing = 9 END 	//	-7 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 40 parameter2 = 2 timing = 9 END 		//	-60% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 145 target = 1 parameter2 = 1 timing = 1 END 	//	no divine spellcasting
	  PHP_EACH d5_no_lowspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 2-4 spells
	  END
	  PHP_EACH d5_no_midspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 5-7 spells
	  END
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 8+ spells
	  END
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul6~ END

	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul7.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 5) parameter2 = 0 timing = 9 END 	//	-5 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 5) parameter2 = 0 timing = 9 END 	//	-5 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 60 parameter2 = 2 timing = 9 END 		//	-40% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 145 target = 1 parameter2 = 1 timing = 1 END 	//	no divine spellcasting
	  PHP_EACH d5_no_midspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 5-7 spells
	  END
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 8+ spells
	  END
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul7~ END

	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul8.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 3) parameter2 = 0 timing = 9 END 	//	-3 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 3) parameter2 = 0 timing = 9 END 	//	-3 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 75 parameter2 = 2 timing = 9 END 		//	-25% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi505~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi607~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi703~ END
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 8+ spells
	  END
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = %mislead_clone_state% parameter2 = 110 timing = 9 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = %lesser_simulacrum_state% parameter2 = 110 timing = 9 STR_VAR resource = ~d5simul8~ END

END

CLEAR_ARRAYS 

END 	//	end define function
//____________________________________________________________________________________


////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////


// -----------------------------
// SPELL TWEAKS: INVISIBILITY REVISIONS
// -----------------------------

// This component uses code by Subtledoctor and is used with permission of the original author. 

// ***** change the way insanctibility works
// ...make a single subspell, and a single 232 .EFF
// ...cast the subspell from all invis spells on a 1-second delay
// ...make a single secondary spell cast by 232, with 172 effects for all spells in the system

DEFINE_ACTION_FUNCTION invisibility_tweaks BEGIN 


//change to invisibility/detect invisible___________________________________________
//
INCLUDE ~%MOD_FOLDER%/lib/d5_insanctibility.tpa~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5__insanctibility.d5~ BEGIN
  LAM D5_INSANCTIBILITY
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN 	// no need to patch 203 if SR installed
  COPY_EXISTING ~spwi203.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 193 target = 1 STR_VAR resource = ~~ END
//	PATCH_FOR_EACH second IN ~6~ ~12~ ~18~ ~24~ ~30~ BEGIN 	//	do this more often
	PATCH_FOR_EACH second IN ~2~ ~4~ ~6~ ~8~ ~10~ ~12~ ~14~ ~16~ ~18~ ~20~ ~22~ ~24~ ~26~ ~28~ ~30~ BEGIN
	  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 116 opcode = 136 timing = 4 duration = %second% END
	END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END
  ACTION_FOR_EACH invisisword IN ~sw1h32.itm~ ~sw1h34.itm~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%invisisword%.itm~ BEGIN
	  COPY_EXISTING ~%invisisword%.itm~ ~override~
		LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 193 target = 1 STR_VAR resource = ~~ END
		LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END
	  BUT_ONLY
	END
  END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN 	// no need to patch 203 if SR installed
  COPY_EXISTING ~spwi203.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 174 match_duration = 30 duration = 60 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 193 match_duration = 30 duration = 60 END
    PATCH_FOR_EACH dur IN ~6~ ~12~ ~18~ ~24~ ~30~ BEGIN
      LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 146 match_duration = %dur% duration = (%dur% - 3) STR_VAR match_resource = ~spwi203d~ END
    END
    PATCH_FOR_EACH new_dur IN ~33~ ~36~ ~39~ ~42~ ~45~ ~48~ ~51~ ~54~ ~57~ ~60~ BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR target = 2 opcode = 146 parameter1 = 1 parameter2 = 1 timing = 4 duration = %new_dur% STR_VAR resource = ~spwi203d~ END 
    END
	READ_STRREF 0x54 ~description~
	PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~5 rounds~) = 0 BEGIN
	  INNER_PATCH_SAVE new_desc ~%description%~ BEGIN
		  OUTER_SET old_duration @150003
		  OUTER_SET new_duration @150004
		REPLACE_TEXTUALLY ~%old_duration%~ ~%new_duration%~
	  END
	  SAY_EVALUATED 0x54 ~%new_desc%~
	END
  IF_EXISTS BUT_ONLY
END

/*
  COPY_EXISTING ~spwi224.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END 
*/
//__________________________________________________________________________________


//change Dispel Illusion to Invisibility Purge______________________________________
//
COPY_EXISTING ~sppr309.spl~ ~override~
  PATCH_FOR_EACH second IN ~6~ ~12~ ~18~ ~24~ ~30~ BEGIN 	//	do this more often
	LPF CLONE_EFFECT INT_VAR match_opcode = 146 match_duration = %second% duration = (%second% - 2) STR_VAR match_resource = ~sppr309d~ END
	LPF CLONE_EFFECT INT_VAR match_opcode = 146 match_duration = %second% duration = (%second% - 4) STR_VAR match_resource = ~sppr309d~ END
  END
IF_EXISTS

COPY_EXISTING ~sppr309.spl~ ~override/spwi322.spl~
	READ_LONG 0x08 spell_name
	READ_LONG 0x50 spell_desc
  	WRITE_SHORT 0x1c 1
  	WRITE_SHORT 0x22 16
  	WRITE_BYTE 0x25 3
  	WRITE_BYTE 0x27 5
  	WRITE_LONG 0x34 3
  	WRITE_BYTE 0x1e (THIS BOR 0b10000000)
IF_EXISTS

COPY_EXISTING ~scrl6k.itm~ ~override~
	WRITE_LONG 0x1c %spell_name%
	WRITE_LONG 0x54 %spell_desc%
	WRITE_ASCII 0x3a ~sppr309a~ #8
IF_EXISTS BUT_ONLY

//__________________________________________________________________________________


//faerie fire/glitterdust counter RI/MI/Blur________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 							// Spell Revisions
  COPY_EXISTING ~sppr114.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 47 opcode = 221 parameter1 = 3 parameter2 = 3 timing = 1 savingthrow = 1 END
  IF_EXISTS BUT_ONLY
END

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_FAERIE_FIRE$~)) BEGIN 	// SoD
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_FAERIE_FIRE~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 221 target = 2 parameter1 = 3 parameter2 = 3 timing = 1 savingthrow = 1 END
  IF_EXISTS BUT_ONLY
END

COPY_EXISTING ~spwi224.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 116 opcode = 221 parameter1 = 3 parameter2 = 3 timing = 1 duration = 0 END
IF_EXISTS BUT_ONLY
//__________________________________________________________________________________



//oracle dispels illusionary creatures_______________________________________________
//
ACTION_IF !FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 	// not needed for Spell Revisions
  COPY_EXISTING ~spwi515.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR target = 2 opcode = 177 parameter1 = 7 parameter2 = 7 timing = 1 STR_VAR resource = ~destself~ END
  IF_EXISTS BUT_ONLY
END
//__________________________________________________________________________________


//true sight = illusionary creature immunity________________________________________
//
// plus a huge luck penalty aura to illusionary creatures
// plus deafness
// and/or caster level penalty? 

COPY_EXISTING ~sppr505.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spwi609.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spcl232.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spcl732.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~sppr505d.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill1~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill2~ END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spwi609d.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill1~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill2~ END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spcl232d.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill1~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 7 parameter2 = 7 timing = 0 duration = 7 STR_VAR resource = ~d5tsill2~ END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~d5tsnewe.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~d5tsnewf.spl~ ~override~
    WRITE_BYTE 0x27 0
IF_EXISTS BUT_ONLY
CREATE EFF ~d5tsill1~
	WRITE_LONG 0x10 22
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c (0 - 5)
	WRITE_LONG 0x20 0
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 7
	WRITE_SHORT 0x2c 100
CREATE EFF ~d5tsill2~
	WRITE_LONG 0x10 80
	WRITE_LONG 0x14 2
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 7
	WRITE_SHORT 0x2c 100
//__________________________________________________________________________________


//handle nondetection spells/items__________________________________________________
//
COPY_EXISTING ~spwi310.spl~ ~override~ 
  SAY NAME1 @6413
  SAY UNIDENTIFIED_DESC @6414
  WRITE_BYTE 0x27 2
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 69 opcode = 205 parameter1 = 0 parameter2 = 5 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 insert_point = 0 STR_VAR resource = ~spwi310~ END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~clck06.itm~ ~override~ 
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 205 target = 1 parameter2 = 5 timing = 2 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 69 END
IF_EXISTS BUT_ONLY

ACTION_IF (MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  COPY_EXISTING ~helm29.itm~ ~override~ 
			  ~helm39.itm~ ~override~ 
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 205 target = 1 parameter2 = 5 timing = 2 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 69 END
  IF_EXISTS BUT_ONLY
END

// ***** do for all nondetection items


//handle IR potions of sight________________________________________________________
//
COPY_EXISTING ~potn30.spl~ ~override~ 
  LPF CLONE_EFFECT INT_VAR match_opcode = 63 opcode = 193 parameter1 = 0 parameter2 = 1 END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~potn30d.spl~ ~override~ 
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 136 target = 1 timing = 1 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 116 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 160 END
IF_EXISTS BUT_ONLY


//make sure invis. purge etc. expose sanctuary______________________________________
//
COPY_EXISTING ~sppr309d.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 116 opcode = 160 END
IF_EXISTS BUT_ONLY


//break sanctuary appropriately_____________________________________________________
//
COPY_EXISTING ~spell.ids~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 1 2 ids_name
	INNER_ACTION BEGIN
	  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL ~%ids_name%~ RET spell_res END
	  COPY_EXISTING ~%spell_res%.spl~ ~override~
		WRITE_BYTE 0x19 (THIS BOR 2)
	  IF_EXISTS BUT_ONLY
	END
  END
BUT_ONLY

ACTION_FOR_EACH ids_label IN
	~CLERIC_BLESS~
	~CLERIC_CURE_LIGHT_WOUNDS~
	~CLERIC_DETECT_EVIL~
	~CLERIC_PROTECT_FROM_EVIL~
	~CLERIC_REMOVE_FEAR~
	~CLERIC_ARMOR_OF_FAITH~
	~CLERIC_SPIRIT_WARD~
	~CLERIC_PROTECT_FROM_GOOD~
	~CLERIC_AID~
	~CLERIC_FIND_TRAPS~
	~CLERIC_GOOD_BERRIES~
	~CLERIC_GOODBERRY~
	~CLERIC_KNOW_ALIGNMENT~
	~CLERIC_RESIST_FIRE~
	~CLERIC_SLOW_POISON~
	~CLERIC_RESIST_FIRE_AND_COLD~
	~CLERIC_CURE_MODERATE_WOUNDS~
	~CLERIC_PROTECTION_FROM_FIRE~
	~CLERIC_REMOVE_CURSE~
	~CLERIC_REMOVE_PARALYSIS~
	~CLERIC_INVISIBILITY_PURGE~
	~CLERIC_CURE_MEDIUM_WOUNDS~
	~CLERIC_CURE_DISEASE~
	~CLERIC_ZONE_OF_SWEET_AIR~
	~CLERIC_EXALTATION~
	~CLERIC_FAVOR_OF_ILMATER~
	~CLERIC_SPIRITUAL_CLARITY~
	~CLERIC_CURE_SERIOUS_WOUNDS~
	~CLERIC_FREE_ACTION~
	~CLERIC_NEUTRALIZE_POISON~
	~CLERIC_DEFENSIVE_HARMONY~
	~CLERIC_PROTECTION_FROM_EVIL_10_FOOT~
	~CLERIC_DEATH_WARD~
	~CLERIC_NEGATIVE_PLANE_PROTECTION~
	~CLERIC_FAR_SIGHT~
	~CLERIC_LESSER_RESTORATION~
	~CLERIC_UNFAILING_ENDURANCE~
	~CLERIC_CURE_CRITICAL_WOUNDS~
	~CLERIC_RAISE_DEAD~
	~CLERIC_TRUE_SIGHT~
	~CLERIC_IRONSKIN~
	~CLERIC_CHAOTIC_COMMANDS~
	~CLERIC_MAGIC_RESISTANCE~
	~CLERIC_MASS_CURE~
	~CLERIC_REPULSE_UNDEAD~
	~CLERIC_PIXIE_DUST~
	~CLERIC_SHIELD_OF_LATHANDER~
	~CLERIC_UNDEAD_WARD~
	~CLERIC_RECALL_SPIRIT~
	~CLERIC_HEAL~
	~CLERIC_WONDROUS_RECALL~
	~CLERIC_PHYSICAL_MIRROR~
	~CLERIC_ENTROPY_SHIELD~
	~CLERIC_SHIELD_OF_THE_ARCHONS~
	~CLERIC_REGENERATE~
	~CLERIC_RESURRECTION~
	~CLERIC_RESTORATION~
	~CLERIC_MASS_RAISE_DEAD~
	~CLERIC_IMPERVIOUS_SANCTITY_OF_MIND~
	~CLERIC_GREATER_SHIELD_OF_LATHANDER~
	~CLERIC_MIST_OF_ELDATH~
	~WIZARD_ARMOR~
	~WIZARD_FRIENDS~
	~WIZARD_IDENTIFY~
	~WIZARD_INFRAVISION~
	~WIZARD_PROTECTION_FROM_EVIL~
	~WIZARD_SHIELD~
	~WIZARD_REFLECT_IMAGE~
	~WIZARD_EXPEDITIOUS_RETREAT~
	~WIZARD_BLUR~
	~WIZARD_DETECT_EVIL~
	~WIZARD_DETECT_INVISIBILITY~
	~WIZARD_INVISIBILITY~
	~WIZARD_KNOW_ALIGNMENT~
	~WIZARD_LUCK~
	~WIZARD_RESIST_FEAR~
	~WIZARD_MIRROR_IMAGE~
	~WIZARD_VOCALIZE~
	~WIZARD_CHAOS_SHIELD~
	~WIZARD_CATS_GRACE~
	~WIZARD_PROTECTION_FROM_PETRIFICATION~
	~WIZARD_CLAIRVOYANCE~
	~WIZARD_INVISIBILITY_10_FOOT~
	~WIZARD_NON_DETECTION~
	~WIZARD_PROTECTION_FROM_NORMAL_MISSILES~
	~WIZARD_GHOST_ARMOR~
	~WIZARD_PROTECTION_FROM_FIRE~
	~WIZARD_PROTECTION_FROM_COLD~
	~WIZARD_DETECT_ILLUSION~
	~WIZARD_IMPROVED_INVISIBILITY~
	~WIZARD_MINOR_GLOBE_OF_INVULNERABILITY~
	~WIZARD_STONE_SKIN~
	~WIZARD_REMOVE_CURSE~
	~WIZARD_SPIRIT_ARMOR~
	~WIZARD_FAR_SIGHT~
	~WIZARD_EYE~
	~WIZARD_EMOTION_HOPE~
	~WIZARD_EMOTION_COURAGE~
	~WIZARD_SHADOW_DOOR~
	~WIZARD_SPELL_IMMUNITY~
	~WIZARD_PROTECTION_FROM_NORMAL_WEAPONS~
	~WIZARD_PROTECTION_FROM_ELECTRICITY~
	~WIZARD_ORACLE~
	~WIZARD_PROTECTION_FROM_ACID~
	~WIZARD_SPELL_SHIELD~
	~WIZARD_MINOR_SPELL_TURNING~
	~WIZARD_GLOBE_OF_INVULNERABILITY~
	~WIZARD_PROTECTION_FROM_MAGIC_ENERGY~
	~WIZARD_TRUE_SIGHT~
	~WIZARD_PROTECTION_FROM_MAGIC_WEAPONS~
	~WIZARD_IMPROVED_HASTE~
	~WIZARD_SPELL_DEFLECTION~
	~WIZARD_STONE_TO_FLESH~
	~WIZARD_TROLLISH_FORTITUDE~
	~WIZARD_SPELL_TURNING~
	~WIZARD_PROTECTION_FROM_THE_ELEMENTS~
	~WIZARD_MANTLE~
	~WIZARD_MASS_INVISIBILITY~
	~WIZARD_IMPROVED_CHAOS_SHIELD~
	~WIZARD_MIND_BLANK~
	~WIZARD_PROTECTION_FROM_ENERGY~
	~WIZARD_IMPROVED_MANTLE~
	~WIZARD_IRON_BODY~
	~WIZARD_SPELL_TRAP~
	~WIZARD_ABSOLUTE_IMMUNITY~
	~WIZARD_FREEDOM~
BEGIN
//  ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]%ids_label%$~)) BEGIN
  ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~ ~[ %TAB%]%ids_label%[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL ~%ids_label%~ RET spell_res END
	COPY_EXISTING ~%spell_res%.spl~ override
	  WRITE_BYTE 0x19 (THIS BAND 253)
	IF_EXISTS BUT_ONLY
  END
END


CLEAR_ARRAYS 

END 	//	end define function
//____________________________________________________________________________________


// -----------------------------
// SPELL TWEAKS: IDENTIFY
// -----------------------------

DEFINE_ACTION_FUNCTION new_identify BEGIN 

ACTION_IF !(FILE_EXISTS_IN_GAME ~qdtnb_identify.qd~) BEGIN

INCLUDE ~TomeAndBlood/lib/b3identify.tph~
LAF B3_IDENTIFY_INSTALL END

LAM identify_spell_level

COPY_EXISTING ~spell.ids~ ~override~
	REPLACE_TEXTUALLY ~WIZARD_IDENTIFY~ ~D5_IDENTIFY_OLD~

ADD_SPELL ~TomeAndBlood/data/spell_tweaks/d5tb110.spl~ 2 1 ~D5_NEW_IDENTIFY~

COPY_EXISTING ~spell.ids~ ~override~
	REPLACE_TEXTUALLY ~D5_NEW_IDENTIFY~ ~WIZARD_IDENTIFY~
	
ACTION_IF FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~WIZARD_IDENTIFY~)	BEGIN
  LAF RES_NUM_OF_SPELL_NAME
	STR_VAR spell_name = ~WIZARD_IDENTIFY~
	RET spell_res
  END

 ACTION_IF (identify_level = 1) BEGIN
  OUTER_SPRINT new_identify_desc @7104
 END

 ACTION_IF (identify_level > 1) BEGIN
  OUTER_SPRINT identify_desc @7104
  OUTER_SPRINT level1_regex @150000
  OUTER_SPRINT new_level @150002
  OUTER_INNER_PATCH_SAVE new_identify_desc ~%identify_desc%~ BEGIN
    REPLACE_TEXTUALLY ~%level1_regex%~ ~%new_level%%identify_level%~
  END
 END

//  COPY ~TomeAndBlood/data/spell_tweaks/d5tb110.spl~ ~override~
 COPY_EXISTING ~%spell_res%.spl~ ~override~
    WRITE_LONG 0x34 %identify_level%
	SAY NAME1 @7103
	SAY_EVALUATED UNIDENTIFIED_DESC ~%new_identify_desc%~

 COPY ~TomeAndBlood/data/spell_tweaks/d5tb110a.spl~ ~override~
 COPY ~TomeAndBlood/data/spell_tweaks/d5tb110b.spl~ ~override~
 COPY ~TomeAndBlood/data/spell_tweaks/d5tb110c.spl~ ~override~
 COPY ~TomeAndBlood/data/spell_tweaks/d5tb110d.spl~ ~override~

 LAF B3_IDENTIFY_REGISTER INT_VAR uses = 1 STR_VAR resref = ~d5tb110a~ END
 LAF B3_IDENTIFY_REGISTER INT_VAR uses = 2 STR_VAR resref = ~d5tb110b~ END
 LAF B3_IDENTIFY_REGISTER INT_VAR uses = 3 STR_VAR resref = ~d5tb110c~ END
 LAF B3_IDENTIFY_REGISTER INT_VAR uses = 4 STR_VAR resref = ~d5tb110d~ END

 ACTION_IF FILE_EXISTS_IN_GAME ~scrl75.itm~ THEN BEGIN
	COPY_EXISTING ~scrl75.itm~ ~override~
		LPF ALTER_ITEM_HEADER INT_VAR target = 5 END
		SAY NAME2 @7103
		SAY IDENTIFIED_DESC ~%new_identify_desc%~
		LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
		LPF ALTER_EFFECT INT_VAR match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END
	BUT_ONLY
 END

 COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	PATCH_IF (%SOURCE_SIZE% > 0x2d3) BEGIN
	  READ_BYTE 0x273 class
	  PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
		REMOVE_KNOWN_SPELL ~spwi110~
		REMOVE_MEMORIZED_SPELL ~spwi110~
		ADD_KNOWN_SPELL ~%spell_res%~ #1 ~wizard~
	  END
	END
 BUT_ONLY

 COPY_EXISTING ~sw1h01.itm~ ~override/qdtnb_identify.qd~ //detection for this component 


 ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
	APPEND ~hidespl.2da~ ~spwi110	1		0		0~
 END
 ACTION_IF (GAME_IS ~iwdee~) BEGIN
	APPEND ~hidespl.2da~ ~spwi110	1		0~
 END

END

END		//	end not found qdtnb_identify.qd

END 	//	end define function
//____________________________________________________________________________________

